promptForBoardSize = function(nextFun, startGameFun)
  clear_screen
  print("Welcome to Minesweeper for GreyScript!")
    size = user_input("How big of board would you like to play with?  Please input a whole number (5 through 25)").to_int

  if (size >= 5 and size <= 25) then
    print("Playing with board size " + size)
    bound = function(difficulty)
      startGameFun(size, difficulty)
    end function

    nextFun(@bound)
  else
    print("Invalid input, prompting again...")
    promptForBoardSize(@nextFun, @startGameFun)
  end if
end function

promptForDifficultyLevel = function(startGameFun)
  difficulty = user_input("What difficulty level would you like to play at? 1, 2 or 3?").to_int

  if (difficulty > 0 and difficulty <= 3) then
    print("Playing at difficulty level " + difficulty)
    startGameFun(difficulty)
  else
    print("Invalid input, prompting again...")
    promptForDifficultyLevel(@startGameFun)
  end if
end function


promptUserForInputs = function(startGameFun)
  promptForBoardSize(@promptForDifficultyLevel, @startGameFun);
end function